buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
		classpath 'org.ajoberstar:grgit:2.1.1'
	}
}

import org.ajoberstar.grgit.*

apply plugin: 'net.minecraftforge.gradle.forge'

ext.repo = Grgit.open()

allprojects {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

version = "${mod_version}"
group = "com.gildedgames"
archivesBaseName = "orbis"

repositories {
	mavenCentral()
	maven {
		url 'https://repo.spongepowered.org/maven/'
	}
	maven {
		url "https://hrznstudio.com/maven/snapshot/"
	}
}

configurations {
	shadeProvided
	shade

	provided.extendsFrom shadeProvided
	compile.extendsFrom shade
}

dependencies {
	shadeProvided 'com.hrznstudio.roadworks:roadworks-sdk-forge:0.0.1.2:api'
	runtime 'com.hrznstudio.roadworks:roadworks-sdk-forge:0.0.1.2:deobf'
    compile project(":OrbisAPI")
}

jar {
	dependsOn(':OrbisAPI:build')
	from sourceSets.main.output
	configurations.shade.each { dep ->
		from(project.zipTree(dep)) {
			exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
		}
	}
	configurations.shadeProvided.each { dep ->
		from(project.zipTree(dep)) {
			exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
		}
	}
	manifest.mainAttributes(
			"Built-By": System.properties['user.name'],
			"Created-By": "${System.properties['java.vm.version']} (${System.properties['java.vm.vendor']})",
			"Maven-Artifact": "$group:$archivesBaseName:$version",
			"ContainedDeps": "${project(":OrbisAPI").mod_name}-${project(":OrbisAPI").mod_version}.jar"
	)
	from("OrbisAPI/build/libs") {
		include("${project(":OrbisAPI").mod_name}-${project(":OrbisAPI").mod_version}.jar")
	}
	classifier = 'universal'
}

minecraft {
	version = forge_version
	mappings = forge_mappings

	runDir = "run"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from (sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

idea {
	module {
		inheritOutputDirs = true
	}
}

// We disable this because Gradle will fail to build otherwise for some reason.
// See https://github.com/MinecraftForge/Srg2Source/issues/10
minecraft {
	makeObfSourceJar = false
}

artifacts {
	archives jar
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    keyStore = System.getenv('GG_SIGN_KEYSTORE')
    alias = System.getenv('GG_SIGN_ALIAS')
    storePass = System.getenv('GG_SIGN_STORE_PASS')
    keyPass = System.getenv('GG_SIGN_KEY_PASS')

    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar