buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle.forge'

allprojects {
	sourceCompatibility = 1.8
	targetCompatibility = 1.8
}

version = "${mod_version}"
group = "com.gildedgames"
archivesBaseName = "orbis"


repositories {
    mavenCentral()
    maven {
	  // location of a maven mirror for JEI files, as a fallback
	  name = "ModMaven"
	  url = "https://modmaven.k-4u.nl"
	}
}

dependencies {
    deobfProvided "mezz.jei:jei_1.12.2:4.15.0.292:api"
    runtime "mezz.jei:jei_1.12.2:4.15.0.292"
    compile project(":OrbisLib")
}

jar {
	dependsOn(':OrbisLib:build')
	from sourceSets.main.output
	manifest {
		attributes 'ContainedDeps': "${project(":OrbisLib").mod_name}-${project(":OrbisLib").mod_version}.jar"
	}
	from("OrbisLib/build/libs") {
		include("${project(":OrbisLib").mod_name}-${project(":OrbisLib").mod_version}.jar")
	}
	classifier = 'universal'
}

minecraft {
	version = forge_version
	mappings = forge_mappings

	runDir = "run"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from (sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'

		expand 'version': project.version, 'mcversion': project.minecraft.version
	}

	from (sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

idea {
	module {
		inheritOutputDirs = true
	}
}

// We disable this because Gradle will fail to build otherwise for some reason.
// See https://github.com/MinecraftForge/Srg2Source/issues/10
minecraft {
	makeObfSourceJar = false
}

task jarNoEmbed(type: Jar) {
	dependsOn(':OrbisLib:build')
	from sourceSets.main.output
	classifier = 'noembed'
}

artifacts {
	archives jar
	archives jarNoEmbed
}

task signJar(type: SignJar, dependsOn: reobfJar) {
    keyStore = System.getenv('GG_SIGN_KEYSTORE')
    alias = System.getenv('GG_SIGN_ALIAS')
    storePass = System.getenv('GG_SIGN_STORE_PASS')
    keyPass = System.getenv('GG_SIGN_KEY_PASS')

    inputFile = jar.archivePath
    outputFile = jar.archivePath
}


if (System.getenv("GRADLE_BUILD_SIGNED") == 'true') {
	build.dependsOn signJar
}
